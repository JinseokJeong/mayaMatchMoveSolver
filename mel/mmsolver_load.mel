//
// Copyright (C) 2022 David Cattermole.
//
// This file is part of mmSolver.
//
// mmSolver is free software: you can redistribute it and/or modify it
// under the terms of the GNU Lesser General Public License as
// published by the Free Software Foundation, either version 3 of the
// License, or (at your option) any later version.
//
// mmSolver is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with mmSolver.  If not, see <https://www.gnu.org/licenses/>.
// ---------------------------------------------------------------------
//
// Install script for mmSolver.
//


proc int mmsolver_get_load_at_startup_envvar() {
    string $load_at_startup = `getenv "MMSOLVER_LOAD_AT_STARTUP"`;
    if ($load_at_startup == "") {
        return 1;
    }
    if ($load_at_startup != "0") {
        return 1;
    }
    return 0;
}


proc int mmsolver_autoload_ask_user() {
    string $yes_button = "Yes";
    string $no_button = "No";
    string $default_button = $no_button;

    int $load_at_startup = mmsolver_get_load_at_startup_envvar();
    if ($load_at_startup == 1) {
        $default_button = $yes_button;
    }

    string $confirm = `confirmDialog
        -title "MM Solver Load"
        -message "Would you like to auto-load MM Solver at Maya start-up?"
        -button $yes_button
        -button $no_button
        -defaultButton $default_button
        -cancelButton $no_button
        -dismissString $no_button`;
    if ($confirm == $yes_button) {
        return 1;
    }
    return 0;
}

global proc mmsolver_load() {
    int $loaded = `pluginInfo -query -loaded "mmSolver"`;
    if ($loaded == 0) {
        loadPlugin "mmSolver";
    }

    int $autoload = 0;
    if (`about -batch` == 1) {
        $autoload = mmsolver_get_load_at_startup_envvar();
    } else {
        $autoload = mmsolver_autoload_ask_user();
    }

    pluginInfo -edit -autoload $autoload "mmSolver";
    if ($autoload == 1) {
        warning -noContext "mmSolver will be automatically loaded at Maya startup.";
    }
}
